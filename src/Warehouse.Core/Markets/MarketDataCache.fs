namespace Warehouse.Core.Markets.Services

open System
open System.Collections.Concurrent
open System.Collections.Frozen
open System.Globalization
open Warehouse.Core.Markets.Contracts
open Warehouse.Core.Markets.Domain

[<Struct>]
type private MarketDataKey = { Symbol: string; MarketType: MarketType }

type private MarketDataSnapshot() =
    member val Asks = ConcurrentDictionary<decimal, struct (decimal * int)>()
    member val Bids = ConcurrentDictionary<decimal, struct (decimal * int)>()

    member this.ToMarketData() = { Asks = this.Asks.ToFrozenDictionary(); Bids = this.Bids.ToFrozenDictionary() }

type MarketDataCache() =
    let cache = ConcurrentDictionary<MarketDataKey, MarketDataSnapshot>()

    let tryParseLevel (data: string[]) =
        if data.Length < 4 then
            ValueNone
        else
            match
                Decimal.TryParse(data[0], CultureInfo.InvariantCulture),
                Decimal.TryParse(data[1], CultureInfo.InvariantCulture),
                Int32.TryParse(data[3], CultureInfo.InvariantCulture)
            with
            | (true, price), (true, size), (true, orderCount) -> ValueSome struct (price, size, orderCount)
            | _ -> ValueNone

    let updateSide (side: ConcurrentDictionary<decimal, struct (decimal * int)>) (levels: string[][]) =
        for level in levels do
            match tryParseLevel level with
            | ValueSome struct (price, size, orderCount) ->
                if size = 0m then side.TryRemove(price) |> ignore else side[price] <- struct (size, orderCount)
            | ValueNone -> ()

    interface IMarketDataCache with
        member _.GetData(symbol, marketType) =
            let key = { Symbol = symbol; MarketType = marketType }

            match cache.TryGetValue(key) with
            | true, snapshot -> Some(snapshot.ToMarketData())
            | false, _ -> None

        member _.Update(marketDataEvent) =
            let key = { Symbol = marketDataEvent.Symbol; MarketType = marketDataEvent.Source }
            let snapshot = cache.GetOrAdd(key, fun _ -> MarketDataSnapshot())

            updateSide snapshot.Asks marketDataEvent.Asks
            updateSide snapshot.Bids marketDataEvent.Bids
