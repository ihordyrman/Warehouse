namespace Warehouse.Core.Workers

open System
open System.Threading
open Microsoft.Extensions.DependencyInjection
open Microsoft.Extensions.Hosting
open Microsoft.Extensions.Logging
open Warehouse.Core.Domain
open Warehouse.Core.Markets.Exchanges.Okx
open Warehouse.Core.Repositories
open Warehouse.Core.Shared.Errors

module CandlestickSync =

    let toPairSymbol (instrument: Instrument) = { Left = instrument; Right = Instrument.USDT }.ToString()

    let toCandlestick (symbol: string) (timeframe: string) (c: OkxCandlestick) : Candlestick =
        {
            Id = 0L
            Symbol = symbol
            MarketType = int MarketType.Okx
            Timestamp = c.Timestamp
            Open = c.Open
            High = c.High
            Low = c.Low
            Close = c.Close
            Volume = c.Volume
            VolumeQuote = c.VolumeQuoteCurrency
            IsCompleted = c.IsCompleted
            Timeframe = timeframe
        }

    let sync
        (http: Http.T)
        (repo: CandlestickRepository.T)
        (logger: ILogger)
        (symbol: string)
        (afterMs: string)
        (limit: int)
        ct
        =
        task {
            let! result =
                http.getCandlesticks symbol { Bar = Some "1m"; Before = None; After = Some afterMs; Limit = Some limit }

            match result with
            | Ok candles when candles.Length > 0 ->
                let mapped = candles |> Array.map (toCandlestick symbol "1m") |> Array.toList
                let! _ = repo.Save mapped ct
                ()
            | Ok _ -> ()
            | Error err -> logger.LogError("Sync failed for {Symbol}: {Error}", symbol, serviceMessage err)
        }


type OkxSynchronizationWorker(scopeFactory: IServiceScopeFactory, logger: ILogger<OkxSynchronizationWorker>) =
    inherit BackgroundService()

    let symbols =
        [|
            Instrument.BTC
            Instrument.ETH
            Instrument.SOL
            Instrument.OKB
            Instrument.DOGE
            Instrument.XRP
            Instrument.BCH
            Instrument.LTC
        |]

    override _.ExecuteAsync(ct) =
        task {
            use scope = scopeFactory.CreateScope()
            let http = scope.ServiceProvider.GetRequiredService<Http.T>()
            let repo = scope.ServiceProvider.GetRequiredService<CandlestickRepository.T>()

            [|
                for i in 0..23 do
                    DateTimeOffset.UtcNow.AddHours(-i).ToUnixTimeMilliseconds().ToString()
            |]
            |> Array.rev
            |> Array.iter (fun after ->
                for instrument in symbols do
                    CandlestickSync.sync http repo logger (CandlestickSync.toPairSymbol instrument) after 60 ct
                    |> Async.AwaitTask
                    |> Async.RunSynchronously
            )

            logger.LogInformation("Initial sync complete")
            use timer = new PeriodicTimer(TimeSpan.FromMinutes 1.0)

            while not ct.IsCancellationRequested do
                let! _ = timer.WaitForNextTickAsync(ct)

                do
                    symbols
                    |> Array.iter (fun instrument ->
                        CandlestickSync.sync
                            http
                            repo
                            logger
                            (CandlestickSync.toPairSymbol instrument)
                            (DateTimeOffset.UtcNow.AddMinutes(-1.0).ToUnixTimeMilliseconds().ToString())
                            10
                            ct
                        |> Async.AwaitTask
                        |> Async.RunSynchronously
                    )
        }
