namespace Warehouse.Core.Repositories

open System
open System.Data
open System.Threading
open Dapper
open Microsoft.Extensions.Logging
open Warehouse.Core.Domain


// type OrderSide =
//     | Buy = 0
//     | Sell = 1
//
// type OrderStatus =
//     | Pending = 0
//     | Placed = 1
//     | PartiallyFilled = 2
//     | Filled = 3
//     | Cancelled = 4
//     | Failed = 5
//
// [<CLIMutable>]
// type Order =
//     {
//         Id: int
//         PipelineId: Nullable<int>
//         MarketType: MarketType
//         ExchangeOrderId: string
//         Symbol: string
//         Side: OrderSide
//         Status: OrderStatus
//         Quantity: decimal
//         Price: Nullable<decimal>
//         StopPrice: Nullable<decimal>
//         Fee: Nullable<decimal>
//         PlacedAt: Nullable<DateTime>
//         ExecutedAt: Nullable<DateTime>
//         CancelledAt: Nullable<DateTime>
//         TakeProfit: Nullable<decimal>
//         StopLoss: Nullable<decimal>
//         CreatedAt: DateTime
//         UpdatedAt: DateTime
//     }

module OrderRepository =

    type CreateOrderRequest =
        {
            PipelineId: int
            MarketType: MarketType
            Symbol: string
            Side: OrderSide
            Quantity: decimal
            Price: decimal
        }

    type SearchFilters =
        {
            SearchTerm: string option
            Side: OrderSide option
            Status: OrderStatus option
            MarketType: MarketType option
            SortBy: string
        }

    type SearchResult = { Orders: Order list; TotalCount: int }

    let getById (db: IDbConnection) (logger: ILogger) (orderId: int) (token: CancellationToken) =
        task {
            try
                let! order =
                    db.QueryFirstOrDefaultAsync<Order>(
                        CommandDefinition(
                            "SELECT * FROM orders WHERE id = @Id",
                            {| Id = orderId |},
                            cancellationToken = token
                        )
                    )

                match box order with
                | null -> return None
                | _ -> return Some order
            with ex ->
                logger.LogError(ex, "Failed to get order {OrderId}", orderId)
                return None
        }

    let getByExchangeId
        (db: IDbConnection)
        (logger: ILogger)
        (exchangeOrderId: string)
        (market: MarketType)
        (token: CancellationToken)
        =
        task {
            try
                let! order =
                    db.QueryFirstOrDefaultAsync<Order>(
                        CommandDefinition(
                            "SELECT * FROM orders WHERE exchange_order_id = @ExchangeOrderId AND market_type = @MarketType",
                            {| ExchangeOrderId = exchangeOrderId; MarketType = int market |},
                            cancellationToken = token
                        )
                    )

                match box order with
                | null -> return None
                | _ -> return Some order
            with ex ->
                logger.LogError(ex, "Failed to get order by exchange id {ExchangeOrderId}", exchangeOrderId)
                return None
        }

    let getByPipeline
        (db: IDbConnection)
        (logger: ILogger)
        (pipelineId: int)
        (status: OrderStatus option)
        (token: CancellationToken)
        =
        task {
            try
                let query =
                    match status with
                    | Some s ->
                        "SELECT * FROM orders WHERE pipeline_id = @PipelineId AND status = @Status ORDER BY created_at DESC"
                    | None -> "SELECT * FROM orders WHERE pipeline_id = @PipelineId ORDER BY created_at DESC"

                let parameters: obj =
                    match status with
                    | Some s -> {| PipelineId = pipelineId; Status = int s |}
                    | None -> {| PipelineId = pipelineId |}

                let! orders = db.QueryAsync<Order>(CommandDefinition(query, parameters, cancellationToken = token))
                return orders |> Seq.toList
            with ex ->
                logger.LogError(ex, "Failed to get orders for pipeline {PipelineId}", pipelineId)
                return []
        }

    let getHistory (db: IDbConnection) (logger: ILogger) (skip: int) (take: int) (token: CancellationToken) =
        task {
            try
                let! orders =
                    db.QueryAsync<Order>(
                        CommandDefinition(
                            "SELECT * FROM orders ORDER BY created_at DESC OFFSET @Skip LIMIT @Take",
                            {| Skip = skip; Take = take |},
                            cancellationToken = token
                        )
                    )

                return orders |> Seq.toList
            with ex ->
                logger.LogError(ex, "Failed to get order history")
                return []
        }

    let getTotalExposure (db: IDbConnection) (logger: ILogger) (market: MarketType option) (token: CancellationToken) =
        task {
            try
                let sql, parameters =
                    match market with
                    | Some m ->
                        """SELECT COALESCE(SUM(quantity * COALESCE(price, 0)), 0)
                           FROM orders
                           WHERE status IN (@Placed, @PartiallyFilled, @Filled)
                           AND market_type = @MarketType""",
                        {|
                            Placed = int OrderStatus.Placed
                            PartiallyFilled = int OrderStatus.PartiallyFilled
                            Filled = int OrderStatus.Filled
                            MarketType = int m
                        |}
                        :> obj
                    | None ->
                        """SELECT COALESCE(SUM(quantity * COALESCE(price, 0)), 0)
                           FROM orders
                           WHERE status IN (@Placed, @PartiallyFilled, @Filled)""",
                        {|
                            Placed = int OrderStatus.Placed
                            PartiallyFilled = int OrderStatus.PartiallyFilled
                            Filled = int OrderStatus.Filled
                        |}
                        :> obj

                let! result =
                    db.QuerySingleAsync<decimal>(CommandDefinition(sql, parameters, cancellationToken = token))

                return result
            with ex ->
                logger.LogError(ex, "Failed to get total exposure")
                return 0m
        }

    let insert
        (db: IDbConnection)
        (txn: IDbTransaction)
        (logger: ILogger)
        (order: CreateOrderRequest)
        (token: CancellationToken)
        =
        task {
            let marketTypeInt = int order.MarketType
            let sideInt = int order.Side
            let statusInt = int OrderStatus.Pending

            let! order =
                db.QuerySingleAsync<Order>(
                    CommandDefinition(
                        """INSERT INTO orders
                           (pipeline_id, market_type, exchange_order_id, symbol, side, status, quantity, price, created_at, updated_at)
                           VALUES (@PipelineId, @MarketType, @ExchangeOrderId, @Symbol, @Side, @Status, @Quantity, @Price, now(), now())
                           RETURNING *""",
                        {|
                            PipelineId = order.PipelineId
                            MarketType = marketTypeInt
                            ExchangeOrderId = ""
                            Symbol = order.Symbol
                            Side = sideInt
                            Status = statusInt
                            Quantity = order.Quantity
                            Price = order.Price
                        |},
                        cancellationToken = token,
                        transaction = txn
                    )
                )

            logger.LogDebug("Inserted order {OrderId}", order.Id)
            return order
        }

    let update (db: IDbConnection) (txn: IDbTransaction) (logger: ILogger) (order: Order) (token: CancellationToken) =
        task {
            let! _ =
                db.ExecuteAsync(
                    CommandDefinition(
                        """UPDATE orders
                           SET status = @Status, quantity = @Quantity, price = @Price, stop_price = @StopPrice,
                               take_profit = @TakeProfit, stop_loss = @StopLoss, exchange_order_id = @ExchangeOrderId,
                               placed_at = @PlacedAt, executed_at = @ExecutedAt, cancelled_at = @CancelledAt,
                               fee = @Fee, updated_at = @UpdatedAt
                           WHERE id = @Id""",
                        {|
                            Id = order.Id
                            Status = int order.Status
                            Quantity = order.Quantity
                            Price = order.Price
                            StopPrice = order.StopPrice
                            TakeProfit = order.TakeProfit
                            StopLoss = order.StopLoss
                            ExchangeOrderId = order.ExchangeOrderId
                            PlacedAt = order.PlacedAt
                            ExecutedAt = order.ExecutedAt
                            CancelledAt = order.CancelledAt
                            Fee = order.Fee
                            UpdatedAt = order.UpdatedAt
                        |},
                        cancellationToken = token,
                        transaction = txn
                    )
                )

            logger.LogDebug("Updated order {OrderId}", order.Id)
        }

    let search
        (db: IDbConnection)
        (logger: ILogger)
        (filters: SearchFilters)
        (skip: int)
        (take: int)
        (token: CancellationToken)
        =
        task {
            try
                let conditions = ResizeArray<string>()
                let parameters = DynamicParameters()

                match filters.SearchTerm with
                | Some term when not (String.IsNullOrEmpty term) ->
                    conditions.Add("symbol ILIKE @SearchTerm")
                    parameters.Add("SearchTerm", $"%%{term}%%")
                | _ -> ()

                match filters.Side with
                | Some side ->
                    conditions.Add("side = @Side")
                    parameters.Add("Side", int side)
                | None -> ()

                match filters.Status with
                | Some status ->
                    conditions.Add("status = @Status")
                    parameters.Add("Status", int status)
                | None -> ()

                match filters.MarketType with
                | Some marketType ->
                    conditions.Add("market_type = @MarketType")
                    parameters.Add("MarketType", int marketType)
                | None -> ()

                let whereClause = if conditions.Count > 0 then "WHERE " + String.Join(" AND ", conditions) else ""

                let orderClause =
                    match filters.SortBy with
                    | "symbol" -> "ORDER BY symbol ASC"
                    | "symbol-desc" -> "ORDER BY symbol DESC"
                    | "status" -> "ORDER BY status ASC"
                    | "status-desc" -> "ORDER BY status DESC"
                    | "side" -> "ORDER BY side ASC"
                    | "side-desc" -> "ORDER BY side DESC"
                    | "quantity" -> "ORDER BY quantity ASC"
                    | "quantity-desc" -> "ORDER BY quantity DESC"
                    | "created-desc" -> "ORDER BY created_at DESC"
                    | "created" -> "ORDER BY created_at ASC"
                    | _ -> "ORDER BY created_at DESC"

                parameters.Add("Skip", skip)
                parameters.Add("Take", take)

                let countSql = $"SELECT COUNT(1) FROM orders {whereClause}"
                let dataSql = $"SELECT * FROM orders {whereClause} {orderClause} OFFSET @Skip LIMIT @Take"

                let! totalCount =
                    db.QuerySingleAsync<int>(CommandDefinition(countSql, parameters, cancellationToken = token))

                let! orders = db.QueryAsync<Order>(CommandDefinition(dataSql, parameters, cancellationToken = token))

                logger.LogDebug("Search returned {Count} orders out of {Total}", Seq.length orders, totalCount)

                return { Orders = orders |> Seq.toList; TotalCount = totalCount }
            with ex ->
                logger.LogError(ex, "Failed to search orders")
                return { Orders = []; TotalCount = 0 }
        }

    let count (db: IDbConnection) (logger: ILogger) (token: CancellationToken) =
        task {
            try
                let! result =
                    db.QuerySingleAsync<int>(
                        CommandDefinition("SELECT COUNT(1) FROM orders", cancellationToken = token)
                    )

                logger.LogDebug("Order count: {Count}", result)
                return result
            with ex ->
                logger.LogError(ex, "Failed to count orders")
                return 0
        }
